apply plugin: 'java'
apply plugin: 'application'

// Use Java 7 by default
sourceCompatibility = '1.7'
targetCompatibility = '1.7'

// The main class of the application
mainClassName = 'com.example.helloworld.HelloWorldService'

buildscript {
    repositories {
        mavenCentral()
    }
}

// Set our project variables
project.ext {
    dropwizardVersion = '0.6.2'
}

repositories {
    mavenCentral()
}

dependencies {
    compile (
       'com.yammer.dropwizard:dropwizard-core:' + dropwizardVersion,
       'com.yammer.dropwizard:dropwizard-hibernate:' + dropwizardVersion,
       'com.yammer.dropwizard:dropwizard-migrations:' + dropwizardVersion,
       'com.yammer.dropwizard:dropwizard-auth:' + dropwizardVersion,
       'com.h2database:h2:1.3.168'
    )
}

// Configure the run task to start the Dropwizard service
run {
    args 'server', './src/dist/config/helloworld.yml'

}

// Create the executable jar and exclude some certificates
jar {

    manifest {
        attributes "Implementation-Title": "DropWizard Quickstart"
        attributes "Main-Class" : mainClassName
    }

    // remove the security files (from mail.jar / activation.jar) so that the jar will be executable.
    doFirst {
        from (configurations.runtime.resolve().collect { it.isDirectory() ? it : zipTree(it) }) {
            exclude 'META-INF/MANIFEST.MF'
            exclude 'META-INF/*.SF'
            exclude 'META-INF/*.DSA'
            exclude 'META-INF/*.RSA'
        }
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '1.6'
}
